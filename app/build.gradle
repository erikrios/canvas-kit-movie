plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

static def getBaseUrl() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['BASE_URL']
}

static def getApiKey() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['API_KEY']
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.erikrios.github.canvaskitmovie"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [BASE_URL: getBaseUrl(), API_KEY: getApiKey()]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "BASE_URL", "\"" + getBaseUrl() + "\""
            variant.buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
}

kapt {
    correctErrorTypes true
}

dependencies {

    //noinspection DifferentStdlibGradleVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.2'

    // Local Unit Tests
    implementation "androidx.test:core:1.3.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'com.google.truth:truth:1.1.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.1"

    // Instrumented Unit Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'com.google.truth:truth:1.1.2'
    androidTestImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.1") {
        // conflicts with mockito due to direct inclusion of byte buddy
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    // Recycler View
    implementation 'androidx.recyclerview:recyclerview:1.2.0-rc01'

    // Navigation Components
    implementation 'android.arch.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'android.arch.navigation:navigation-ui-ktx:2.3.0'

    // Architectural Components
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Room Database
    implementation 'androidx.room:room-ktx:2.2.6'
    kapt 'androidx.room:room-compiler:2.2.6'

    // DataStore Preferences
    implementation 'androidx.datastore:datastore-preferences:1.0.0-alpha08'

    // Kenburns View
    implementation 'com.flaviofaria:kenburnsview:1.0.7'

    // Circle Image View
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Dagger Hilt
    implementation 'com.google.dagger:hilt-android:2.33-beta'
    kapt 'com.google.dagger:hilt-compiler:2.33-beta'
    kapt "com.google.dagger:hilt-android-compiler:2.33-beta"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03'

    // For instrumentation tests (Dagger Hilt)
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.33-beta'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.33-beta'

    // For local unit tests (Dagger Hilt)
    testImplementation 'com.google.dagger:hilt-android-testing:2.33-beta'
    kaptTest 'com.google.dagger:hilt-compiler:2.33-beta'

    // Lottie
    implementation 'com.airbnb.android:lottie:3.4.0'
}